import vertex.stage;
import fragment.stage;


import rendering.shaderSignature;
import rendering.environment;
import rendering.depthNormalFragmentOutput;
import rendering.lightingOutputs;

import vertex.genericLayout;

import texture.functions;

uniform(binding=MaterialState.Attributes) MaterialState
{
    clipMapPeriod: float2;

    heightMapScale: float2;
    heightMapOffset: float2;

    heightScale: float;
    heightOffset: float;

    albedo: float4;
    metallicFactor: float;
    roughnessFactor: float;

    textureScaleAndOffset: float4[5];
}

uniform(binding = MaterialState.Textures) heightMapTexture : texture2D;
uniform(binding = MaterialState.Textures + 1) splattingTexture : texture2D;

uniform(binding = MaterialState.Textures + 2) albedoTexture0 : texture2D;
uniform(binding = MaterialState.Textures + 3) albedoTexture1 : texture2D;
uniform(binding = MaterialState.Textures + 4) albedoTexture2 : texture2D;
uniform(binding = MaterialState.Textures + 5) albedoTexture3 : texture2D;
uniform(binding = MaterialState.Textures + 6) albedoTexture4 : texture2D;

uniform(binding = MaterialState.Textures + 7) normalTexture0 : texture2D;
uniform(binding = MaterialState.Textures + 8) normalTexture1 : texture2D;
uniform(binding = MaterialState.Textures + 9) normalTexture2 : texture2D;
uniform(binding = MaterialState.Textures + 10) normalTexture3 : texture2D;
uniform(binding = MaterialState.Textures + 11) normalTexture4 : texture2D;

function surfacePositionIntoTexcoord(position: float3) -> float2
{
    return float2(position.x, -position.z)*0.5f*MaterialState.heightMapScale + 0.5f + MaterialState.heightMapOffset;
}

function surfaceTexcoordIntoPosition(texcoord: float2) -> float3
{
    let result = (texcoord - MaterialState.heightMapOffset - 0.5f)*2.0f/MaterialState.heightMapScale;
    return float3(result.x, 0.0f, -result.y);
}

function mixSplatting(v0: float4, v1: float4, v2: float4, v3: float4, v4: float4, alpha: float4) -> float4
{
    let a0 = 1.0f - dot(alpha, float4(1.0f, 1.0f, 1.0f, 1.0f));
    return v0*a0 + v1*alpha.x + v2*alpha.y + v3*alpha.z + v4*alpha.w;
}

function mixSplatting(v0: float3, v1: float3, v2: float3, v3: float3, v4: float3, alpha: float4) -> float3
{
    let a0 = 1.0f - dot(alpha, float4(1.0f, 1.0f, 1.0f, 1.0f));
    return v0*a0 + v1*alpha.x + v2*alpha.y + v3*alpha.z + v4*alpha.w;
}

function transformNormalToView(normal: float3) -> float3
{
    using CameraState;
    using ObjectState;
    using InstanceObjectState;

    return ((float4(normal, 0.0f) * objectState.inverseMatrix) * inverseViewMatrix).xyz;
}

function fetchAlbedoPlane(texcoord: float2, splatting: float4) -> float4
{
    using MaterialState;

    let a0 = albedoSampler.sample(albedoTexture0, texcoord*textureScaleAndOffset[0].xy + textureScaleAndOffset[0].zw);
    let a1 = albedoSampler.sample(albedoTexture1, texcoord*textureScaleAndOffset[1].xy + textureScaleAndOffset[1].zw);
    let a2 = albedoSampler.sample(albedoTexture2, texcoord*textureScaleAndOffset[2].xy + textureScaleAndOffset[2].zw);
    let a3 = albedoSampler.sample(albedoTexture3, texcoord*textureScaleAndOffset[3].xy + textureScaleAndOffset[3].zw);
    let a4 = albedoSampler.sample(albedoTexture4, texcoord*textureScaleAndOffset[4].xy + textureScaleAndOffset[4].zw);

    return mixSplatting(a0, a1, a2, a3, a4, splatting);
}

function mixTriplanar(normal: float3, vx: float4, vy: float4, vz: float4) -> float4
{
    let blending = fabs(normal);
    blending = blending / (blending.x + blending.y + blending.z);

    return vx*blending.x + vy*blending.y + vz*blending.z;
}

function mixTriplanarNormal(normal: float3, vx: float3, vy: float3, vz: float3) -> float3
{
    let blending = normalize(fabs(normal));

    return vx*blending.x + vy*blending.y + vz*blending.z;
}

function fetchAlbedo(objectPosition: float3, normal: float3, splatting: float4) -> float4
{
    let vx = fetchAlbedoPlane(objectPosition.yz, splatting);
    let vy = fetchAlbedoPlane(objectPosition.zx, splatting);
    let vz = fetchAlbedoPlane(objectPosition.xy, splatting);

    return mixTriplanar(normal, vx, vy, vz);
}

function fetchNormalPlane(texcoord: float2, splatting: float4) -> float3
{
    using MaterialState;

    let n0 = decodeNormalTexel(normalSampler.sample(normalTexture0, texcoord*textureScaleAndOffset[0].xy + textureScaleAndOffset[0].zw));
    let n1 = decodeNormalTexel(normalSampler.sample(normalTexture1, texcoord*textureScaleAndOffset[1].xy + textureScaleAndOffset[1].zw));
    let n2 = decodeNormalTexel(normalSampler.sample(normalTexture2, texcoord*textureScaleAndOffset[2].xy + textureScaleAndOffset[2].zw));
    let n3 = decodeNormalTexel(normalSampler.sample(normalTexture3, texcoord*textureScaleAndOffset[3].xy + textureScaleAndOffset[3].zw));
    let n4 = decodeNormalTexel(normalSampler.sample(normalTexture4, texcoord*textureScaleAndOffset[4].xy + textureScaleAndOffset[4].zw));

    return mixSplatting(n0, n1, n2, n3, n4, splatting);
}

function fetchNormalPlaneYZ(texcoord: float2, splatting: float4, objectNormalSigns: float3) -> float3
{
    return fetchNormalPlane(texcoord, splatting).zxy*objectNormalSigns;
}

function fetchNormalPlaneZX(texcoord: float2, splatting: float4, objectNormalSigns: float3) -> float3
{
    return fetchNormalPlane(texcoord, splatting).yzx*objectNormalSigns;
}

function fetchNormalPlaneXY(texcoord: float2, splatting: float4, objectNormalSigns: float3) -> float3
{
    return fetchNormalPlane(texcoord, splatting).xyz*objectNormalSigns;
}

function fetchNormal(objectPosition: float3, normal: float3, splatting: float4) -> float3
{
    let normalSigns = step(0.0f, normal)*2.0f - 1.0f;
    let vx = fetchNormalPlaneYZ(objectPosition.yz, splatting, normalSigns);
    let vy = fetchNormalPlaneZX(objectPosition.zx, splatting, normalSigns);
    let vz = fetchNormalPlaneXY(objectPosition.xy, splatting, normalSigns);

    return mixTriplanarNormal(normal, vx, vy, vz);
}

//==============================================================================
// Vertex and fragment shader interface

semantic VertexOutput {
    out(0) position: float3;
    out(1) surfaceTexcoord: float2;
    out(2) objectPosition: float3;
    out(3) color: float4;
}

semantic FragmentInput {
    in(0) position: float3;
    in(1) surfaceTexcoord: float2;
    in(2) objectPosition: float3;
    in(3) color: float4;
}

//==============================================================================
// Vertex shader

code_block(vertex) vertex
{
    using CameraState;
    using MaterialState;

    let position = float3(GenericVertexLayout.position.x, 0.0f,  GenericVertexLayout.position.y) + clipMapOffsetWithPeriod(clipMapPeriod);
    let lod = GenericVertexLayout.position.z;

    let surfaceTexcoord = surfacePositionIntoTexcoord(position);

    if(isCurrentObjectInstanceInvisible() /*|| surfaceTexcoord.s <= -0.1f || surfaceTexcoord.t <= -0.1f || surfaceTexcoord.s >= 1.1f || surfaceTexcoord.t >= 1.1f*/)
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }


    // Compute the height
    let height = displacementSampler.sampleLod(heightMapTexture, surfaceTexcoord, lod).r*heightScale + heightOffset;

    position += float3(0.0f, height, 0.0f);

    VertexOutput.color = currentObjectInstanceColor();
    /*if(lod == 0.0f)
        VertexOutput.color = float4(1.0f, 0.0f, 0.0f, 1.0f);
    else if(lod == 1.0f)
        VertexOutput.color = float4(0.0f, 1.0f, 0.0f, 1.0f);
    else if(lod == 2.0f)
        VertexOutput.color = float4(0.0f, 0.0f, 1.0f, 1.0f);*/

    VertexOutput.surfaceTexcoord = surfaceTexcoord;
    VertexOutput.objectPosition = position;
    let position4 = transformPositionToView(position);

    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

//==============================================================================
// Depth normal output

code_block(fragment) depthNormal_fragment
{
    let dh = 0.5f*MaterialState.heightMapScale;

    let surfaceTexcoord = FragmentInput.surfaceTexcoord;
    let objectPosition = FragmentInput.objectPosition;

    if(surfaceTexcoord.s < 0.0f || surfaceTexcoord.t < 0.0f || surfaceTexcoord.s > 1.0f || surfaceTexcoord.t > 1.0f)
        discard;

    // Compute the height map derivatives
    let dx = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(dh.x, 0.0f)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(dh.x, 0.0f)).r;
    let dy = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(0.0f, dh.y)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(0.0f, dh.y)).r;

    // Compute the object space normal.
    let on = normalize(float3(-dx*MaterialState.heightScale, 1.0f, dy*MaterialState.heightScale));
    let SN = transformNormalToView(on);
    encodeViewNormalOutput(normalize(SN));
}

//==============================================================================
// Forward lighting model.

code_block(fragment) forward_splatting
{
    let dh = 0.5f*MaterialState.heightMapScale;

    let surfaceTexcoord = FragmentInput.surfaceTexcoord;
    let objectPosition = FragmentInput.objectPosition;

    if(surfaceTexcoord.s < 0.0f || surfaceTexcoord.t < 0.0f || surfaceTexcoord.s > 1.0f || surfaceTexcoord.t > 1.0f)
        discard;

    // Fetch the splatting
    let splatting = albedoSampler.sample(splattingTexture, surfaceTexcoord);

    // Compute the height map derivatives
    let dx = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(dh.x, 0.0f)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(dh.x, 0.0f)).r;
    let dy = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(0.0f, dh.y)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(0.0f, dh.y)).r;

    // Compute the object space normal.
    let on = normalize(float3(-dx*MaterialState.heightScale, 1.0f, dy*MaterialState.heightScale));

    // Fetch the albedo.
    let rawAlbedo = fetchAlbedo(objectPosition, on, splatting);
    let albedo = FragmentInput.color*MaterialState.albedo*float4(rawAlbedo.rgb, 1.0f);

    // Fetch the actual normal
	//walbedo = 1.0f;
	//on = float3(-1.0f, 0.0f, 0.0f);
    let rawNormal = fetchNormal(objectPosition, on, splatting);
    let N = normalize(transformNormalToView(rawNormal));

    let SN = transformNormalToView(on);

    // Compute the smoothness and the fresnel factor.
	let metallic = MaterialState.metallicFactor;
	let roughness = MaterialState.roughnessFactor;

    let V = normalize(-FragmentInput.position);

    forwardMetallicRoughnessLightingOutput
        emission: 0.0f albedo: albedo
        metallic: metallic roughness: roughness
        normal: N surfaceNormal: SN viewVector: V position: FragmentInput.position;
    //FragmentStage.colorOutput0 = float4(on*0.5f + 0.5f, 1.0f);
    //FragmentStage.colorOutput0 = float4(on, 1.0f);
    //FragmentStage.colorOutput0 = float4(SN*0.5f + 0.5f, 1.0f);
}

code_block(fragment) forward_solid
{
    let dh = 0.5f*MaterialState.heightMapScale;

    let surfaceTexcoord = FragmentInput.surfaceTexcoord;
    let objectPosition = FragmentInput.objectPosition;

    if(surfaceTexcoord.s < 0.0f || surfaceTexcoord.t < 0.0f || surfaceTexcoord.s > 1.0f || surfaceTexcoord.t > 1.0f)
        discard;

    // Fetch the splatting
    let splatting = albedoSampler.sample(splattingTexture, surfaceTexcoord);

    // Compute the height map derivatives
    let dx = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(dh.x, 0.0f)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(dh.x, 0.0f)).r;
    let dy = displacementSampler.sample(heightMapTexture, surfaceTexcoord + float2(0.0f, dh.y)).r - displacementSampler.sample(heightMapTexture, surfaceTexcoord - float2(0.0f, dh.y)).r;

    // Compute the object space normal
    let on = normalize(float3(-dx*MaterialState.heightScale, 1.0f, dy*MaterialState.heightScale));

    // Fetch the albedo.
    let albedo = FragmentInput.color*MaterialState.albedo;

    // Fetch the actual normal
    let N = transformNormalToView(on);

	// For double sided material.
    let V = normalize(-FragmentInput.position);
		
    // Compute the smoothness and the fresnel factor.
	let metallic = MaterialState.metallicFactor;
	let roughness = MaterialState.roughnessFactor;

    forwardMetallicRoughnessLightingOutput
        emission: 0.0f albedo: albedo
        metallic: metallic roughness: roughness
        normal: N surfaceNormal: N viewVector: V position: FragmentInput.position;
}

code_block(fragment) forward_wire
{
    let surfaceTexcoord = FragmentInput.surfaceTexcoord;
    if(surfaceTexcoord.s < 0.0f || surfaceTexcoord.t < 0.0f || surfaceTexcoord.s > 1.0f || surfaceTexcoord.t > 1.0f)
        discard;

    forwardFlatOutput color: FragmentInput.color*MaterialState.albedo;
}

code_block(fragment) forward_isolines
{
    let surfaceTexcoord = FragmentInput.surfaceTexcoord;
    if(surfaceTexcoord.s < 0.0f || surfaceTexcoord.t < 0.0f || surfaceTexcoord.s > 1.0f || surfaceTexcoord.t > 1.0f)
        discard;

    // Fetch the height
    let height = displacementSampler.sample(heightMapTexture, surfaceTexcoord).r*MaterialState.heightScale;
    // TODO: Use derivatives here.
    let isolinePeriod = 1.0f - smoothstep(0.0f, 0.06f, fract(height *0.25f));

    forwardFlatOutput color: FragmentInput.color*isolinePeriod;
}
