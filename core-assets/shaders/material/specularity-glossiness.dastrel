import vertex.stage;
import fragment.stage;

import rendering.shaderSignature;
import rendering.environment;
import rendering.depthNormalFragmentOutput;
import rendering.lightingOutputs;

import vertex.genericLayout;
import vertex.genericSkinnedLayout;

import texture.functions;

//==============================================================================
// Material state

uniform(binding = MaterialState.Attributes) MaterialState
{
    emission: float4;
    albedo: float4;
    specularFactor: float3;
    glossinessFactor: float;
	alphaCutoff: float;
}

uniform(binding = MaterialState.Textures) albedoTexture : texture2D;
uniform(binding = MaterialState.Textures + 1) normalTexture : texture2D;
uniform(binding = MaterialState.Textures + 2) emissionTexture : texture2D;
uniform(binding = MaterialState.Textures + 3) specularGlosinessTexture : texture2D;

//==============================================================================
// Vertex and fragment shader interface

semantic VertexOutput {
    out(0) position: float3;
    out(1) texcoord: float2;
    out(2) color: float4;
    out(3) normal: float3;
    out(4) tangent: float3;
    out(5) bitangent: float3;
}

semantic FragmentInput {
    in(0) position: float3;
    in(1) texcoord: float2;
    in(2) color: float4;
    in(3) normal: float3;
    in(4) tangent: float3;
    in(5) bitangent: float3;
}

//==============================================================================
// Vertex shaders

code_block(vertex) depth_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.normal = transformNormalToView(GenericVertexLayout.normal);

    let position4 = transformPositionToView(GenericVertexLayout.position);
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) flat_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.normal = transformNormalToView(GenericVertexLayout.normal);

    let position4 = transformPositionToView(GenericVertexLayout.position);
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) normal_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.tangent = transformNormalToView(GenericVertexLayout.tangent4.xyz);
    VertexOutput.normal = transformNormalToView(GenericVertexLayout.normal);
    VertexOutput.bitangent = cross(VertexOutput.normal, VertexOutput.tangent) * GenericVertexLayout.tangent4.w;

    let position4 = transformPositionToView(GenericVertexLayout.position);
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) color_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = GenericVertexLayout.color*currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.normal = transformNormalToView(GenericVertexLayout.normal);

    let position4 = transformPositionToView(GenericVertexLayout.position);
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) colorNormal_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = GenericVertexLayout.color*currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.tangent = transformNormalToView(GenericVertexLayout.tangent4.xyz);
    VertexOutput.normal = transformNormalToView(GenericVertexLayout.normal);
    VertexOutput.bitangent = cross(VertexOutput.normal, VertexOutput.tangent) * GenericVertexLayout.tangent4.w;

    let position4 = transformPositionToView(GenericVertexLayout.position);
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

function skinPosition(position: float3) -> float3
{
    return skinPositionWithBones(position, SkinnedGenericVertexLayout.boneIndices, SkinnedGenericVertexLayout.boneWeights);
}

function skinVector(vector: float3) -> float3
{
    return skinVectorWithBones(vector, SkinnedGenericVertexLayout.boneIndices, SkinnedGenericVertexLayout.boneWeights);
}

code_block(vertex) depthSkinned_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.normal = transformNormalToView(skinVector(GenericVertexLayout.normal));

    let position4 = transformPositionToView(skinPosition(GenericVertexLayout.position));
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) skinnedColorNormal_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = GenericVertexLayout.color*currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.tangent = transformNormalToView(skinVector(GenericVertexLayout.tangent4.xyz));
    VertexOutput.normal = transformNormalToView(skinVector(GenericVertexLayout.normal));
    VertexOutput.bitangent = cross(VertexOutput.normal, VertexOutput.tangent) * GenericVertexLayout.tangent4.w;

    let position4 = transformPositionToView(skinPosition(GenericVertexLayout.position));
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) skinnedColor_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = GenericVertexLayout.color*currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.normal = transformNormalToView(skinVector(GenericVertexLayout.normal));

    let position4 = transformPositionToView(skinPosition(GenericVertexLayout.position));
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) skinnedNormal_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.tangent = transformNormalToView(skinVector(GenericVertexLayout.tangent4.xyz));
    VertexOutput.normal = transformNormalToView(skinVector(GenericVertexLayout.normal));
    VertexOutput.bitangent = cross(VertexOutput.normal, VertexOutput.tangent) * GenericVertexLayout.tangent4.w;

    let position4 = transformPositionToView(skinPosition(GenericVertexLayout.position));
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

code_block(vertex) skinned_vertex
{
    using CameraState;

    if(isCurrentObjectInstanceInvisible())
    {
        VertexStage.screenPosition = float4(2.0f, 2.0f, -2.0f, 1.0f);
        return;
    }

    VertexOutput.color = currentObjectInstanceColor();
    VertexOutput.texcoord = GenericVertexLayout.texcoord;
    VertexOutput.normal = transformNormalToView(skinVector(GenericVertexLayout.normal));

    let position4 = transformPositionToView(skinPosition(GenericVertexLayout.position));
    VertexOutput.position =  position4.xyz;
    VertexStage.screenPosition = projectionMatrix * position4;
}

//==============================================================================
// Depth normal

code_block(fragment) depthNormal_fragment
{
	if(FragmentInput.color.a < MaterialState.alphaCutoff)
		discard;

    encodeViewNormalOutput(normalize(FragmentInput.normal));
}

code_block(fragment) depthNormalMask_fragment
{
    let alpha = FragmentInput.color.a * albedoSampler.sample(albedoTexture, FragmentInput.texcoord).a;
	if(alpha < MaterialState.alphaCutoff)
		discard;

    encodeViewNormalOutput(normalize(FragmentInput.normal));
}

//==============================================================================
// Forward lighting model.

code_block(fragment) forward_color
{
    let N = normalize(FragmentInput.normal);
    let V = normalize(-FragmentInput.position);

	let emission = MaterialState.emission*albedoSampler.sample(emissionTexture, FragmentInput.texcoord);
    forwardSpecularityGlossinessLightingOutput
        emission: emission albedo: FragmentInput.color*MaterialState.albedo
        specular: MaterialState.specularFactor glossiness: MaterialState.glossinessFactor
        normal: N surfaceNormal: N viewVector: V position: FragmentInput.position;
}

code_block(fragment) forward_albedo
{
    let N = normalize(FragmentInput.normal);
    let V = normalize(-FragmentInput.position);

	let emission = MaterialState.emission*albedoSampler.sample(emissionTexture, FragmentInput.texcoord);
    let albedo = FragmentInput.color * albedoSampler.sample(albedoTexture, FragmentInput.texcoord);
    forwardSpecularityGlossinessLightingOutput
        emission: emission albedo: albedo*MaterialState.albedo
        specular: MaterialState.specularFactor glossiness: MaterialState.glossinessFactor
        normal: N surfaceNormal: N viewVector: V position: FragmentInput.position;
}


code_block(fragment) forward_albedoNormal
{
    let t = normalize(FragmentInput.tangent);
    let b = normalize(FragmentInput.bitangent);
    let n = normalize(FragmentInput.normal);
    let V = normalize(-FragmentInput.position);

	let emission = MaterialState.emission*albedoSampler.sample(emissionTexture, FragmentInput.texcoord);
    let albedo = FragmentInput.color * albedoSampler.sample(albedoTexture, FragmentInput.texcoord);
    let tangentNormal = decodeNormalTexel(normalSampler.sample(normalTexture, FragmentInput.texcoord));
    let TBN = float3x3(t, b, n);
    let N = normalize(TBN*tangentNormal);

    forwardSpecularityGlossinessLightingOutput
        emission: emission albedo: albedo*MaterialState.albedo
        specular: MaterialState.specularFactor glossiness: MaterialState.glossinessFactor
        normal: N surfaceNormal: n viewVector: V position: FragmentInput.position;
}

code_block(fragment) forward_albedoSpecularGlosiness
{
    let N = normalize(FragmentInput.normal);
    let V = normalize(-FragmentInput.position);

	let emission = MaterialState.emission*albedoSampler.sample(emissionTexture, FragmentInput.texcoord);
    let albedo = FragmentInput.color * albedoSampler.sample(albedoTexture, FragmentInput.texcoord);
    let specularGlosiness = albedoSampler.sample(specularGlosinessTexture, FragmentInput.texcoord);
    forwardSpecularityGlossinessLightingOutput
        emission: emission albedo: albedo*MaterialState.albedo
        specular: specularGlosiness.rgb*MaterialState.specularFactor glossiness: specularGlosiness.a*MaterialState.glossinessFactor
        normal: N surfaceNormal: N viewVector: V position: FragmentInput.position;
}

code_block(fragment) forward_albedoNormalSpecularGlosiness
{
    let t = normalize(FragmentInput.tangent);
    let b = normalize(FragmentInput.bitangent);
    let n = normalize(FragmentInput.normal);
    let V = normalize(-FragmentInput.position);

	let emission = MaterialState.emission*albedoSampler.sample(emissionTexture, FragmentInput.texcoord);
    let albedo = FragmentInput.color * albedoSampler.sample(albedoTexture, FragmentInput.texcoord);
    let specularGlosiness = albedoSampler.sample(specularGlosinessTexture, FragmentInput.texcoord);
    let tangentNormal = decodeNormalTexel(normalSampler.sample(normalTexture, FragmentInput.texcoord));
    let TBN = float3x3(t, b, n);
    let N = normalize(TBN*tangentNormal);

    forwardSpecularityGlossinessLightingOutput
        emission: emission albedo: albedo*MaterialState.albedo
        specular: specularGlosiness.rgb*MaterialState.specularFactor glossiness: specularGlosiness.a*MaterialState.glossinessFactor
        normal: N surfaceNormal: n viewVector: V position: FragmentInput.position;
}
