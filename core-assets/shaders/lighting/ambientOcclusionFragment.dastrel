import fragment.stage;
import fragment.screenQuad;
import procedural.noise;

shader_signature AmbientOcclusion
{
    descriptor_set(max_bindings=10) Samplers
    {
        Samplers: sampler*1;
    }

    descriptor_set(max_bindings = 1000) CameraState
    {
        ObjectState : uniform;
		PostProcessingState : buffer;
    }

    descriptor_set(max_bindings = 1000) ComputeBuffers
    {
        DepthBuffer: texture;
        NormalBuffer: texture;
        Destination: image;
    }
}

uniform(binding=CameraState.ObjectState) CameraState
{
    inverseViewMatrix: float4x4;
    viewMatrix: float4x4;

    projectionMatrix: float4x4;
    inverseProjectionMatrix: float4x4;

    screenSize: float2;
    inverseScreenSize: float2;

    currentTime: float;
}

uniform(binding = Samplers.Samplers) aoDepthSampler : sampler;
uniform(binding = Samplers.Samplers) aoNormalSampler : sampler;

uniform(binding = ComputeBuffers.DepthBuffer) depthTexture : texture2D;
uniform(binding = ComputeBuffers.NormalBuffer) normalTexture : texture2D;

constant SamplingDistributionSize : int = 8;
constant SamplingDistribution : float3[8] = {
    { -0.6877715587615967f, -0.506843626499176f, 0.5196920037269592f },
    { -0.34024709463119507f, 0.2696249485015869f, 0.9008519649505615f },
    { 0.8366008996963501f, -0.2242094725370407f, 0.4998289942741394f },
    { 0.5788835287094116f, -0.8140687346458435f, 0.04675422981381416f },
    { 0.1709364503622055f, 0.9850329756736755f, 0.022153066471219063f },
    { 0.8136236667633057f, 0.519911527633667f, 0.2602085471153259f },
    { -0.8440455198287964f, 0.44032758474349976f, 0.3061026632785797f },
    { 0.1833304911851883f, -0.7002113461494446f, 0.6899957060813904f },
};

/*constant SamplingDistributionSize : int = 16;
constant SamplingDistribution : float3[16] = {
    { -0.6877715587615967f, -0.506843626499176f, 0.5196920037269592f },
    { -0.34024709463119507f, 0.2696249485015869f, 0.9008519649505615f },
    { -0.762890636920929f, 0.301253080368042f, 0.5720528960227966f },
    { 0.8366008996963501f, -0.2242094725370407f, 0.4998289942741394f },
    { 0.5788835287094116f, -0.8140687346458435f, 0.04675422981381416f },
    { 0.1709364503622055f, 0.9850329756736755f, 0.022153066471219063f },
    { -0.2356363832950592f, -0.8754839897155762f, 0.4219043552875519f },
    { 0.3668358027935028f, 0.17475688457489014f, 0.9137240052223206f },
    { 0.8136236667633057f, 0.519911527633667f, 0.2602085471153259f },
    { -0.23676802217960358f, -0.44476839900016785f, 0.8637835383415222f },
    { -0.31475013494491577f, 0.8464008569717407f, 0.4295788109302521f },
    { 0.31952965259552f, 0.7079995274543762f, 0.6297916173934937f },
    { -0.8757982850074768f, -0.48266661167144775f, 0.0032114710193127394f },
    { 0.4089246392250061f, -0.4607699513435364f, 0.7877002358436584f },
    { -0.8133195638656616f, 0.5813238620758057f, 0.023954791948199272f },
    { -0.988899290561676f, 0.08517222851514816f, 0.1217532530426979f },
};
*/

function rotationMatrixFor(s: float) -> float3x3
{
	let c = sqrt(1.0f - s*s);
    return float3x3(
        float3(c, s, 0.0f),
        float3(-s, c, 0.0f),
        float3(0.0f, 0.0f, 1.0f)
    );
}

constant vectorAxis : float3[6] = {
    {1.0f, 0.0f, 0.0f},
    {0.0f, 1.0f, 0.0f},
    {0.0f, 0.0f, 1.0f},
    {1.0f, 0.0f, 0.0f},
    {0.0f, 1.0f, 0.0f},
    {0.0f, 0.0f, 1.0f},
};

function findBestAxisFor(v: float3) -> int
{
    let bestDot = fabs(dot(v, vectorAxis[0]));
    let bestAxis = 0;

    let d = fabs(dot(v, vectorAxis[1]));
    if(d > bestDot)
    {
        bestDot = d;
        bestAxis = 1;
    }

    d = fabs(dot(v, vectorAxis[2]));
    if(d > bestDot)
        bestAxis = 2;

    return bestAxis;
}

function computeTBNForNormal(normal: float3) -> float3x3
{
    let normalAxis = findBestAxisFor(normal);

    // Axis[normalAxis + 1] and axis[normalAxis + 2] are not parallel to the
    // normal value. However, they do not necesarily form an orthogonal basis,
    // so we apply the Gram-schmidth orthogonalization.
    let tangent = vectorAxis[normalAxis + 1];
    tangent = normalize(tangent - normal*dot(normal, tangent));

    let bitangent = vectorAxis[normalAxis + 2];
    bitangent = normalize(bitangent - normal*dot(normal, bitangent) - tangent*dot(tangent, bitangent));

    return float3x3(tangent, bitangent, normal);
}

function sampleAmbientOcclusionAt(eyeDepth: float, position: float3) -> float
{
    let projectedPosition = CameraState.projectionMatrix * float4(position, 1.0f);
    let samplePoint = projectedPosition.xyz / projectedPosition.w;
    let sampleDepth = aoDepthSampler.sampleLod(depthTexture, samplePoint.xy*0.5f + 0.5f, 0.0f).r;

    // Large variations of depth can introduce some false positives.
    let threshold = fabs(eyeDepth - samplePoint.z)*10.0f;
    let attenuation = 1.0f - smoothstep(threshold, threshold*2.0f, fabs(sampleDepth - samplePoint.z));
    return (sampleDepth - 0.00001f < samplePoint.z ? 0.0f : 1.0f)*attenuation;
}

code_block(fragment) ssao
{
    let uv = FragmentInput.texcoord;

    // Compute the eye position, in view space.
    let eyeDepth = aoDepthSampler.sampleLod(depthTexture, uv, 0.0f).r;
    let screenPosition = float4(uv*2.0f - 1.0f, eyeDepth, 1.0f);

    let eyePosition4 = CameraState.inverseProjectionMatrix *screenPosition;
    let eyePosition = eyePosition4.xyz/eyePosition4.w;

    // Read the normal
    let eyeNormal = normalize(aoNormalSampler.sampleLod(normalTexture, uv, 0.0f).rgb*2.0f - 1.0f);

    // Compute the TBN matrix.
    let normalAxis = findBestAxisFor(eyeNormal);
    let TBN = computeTBNForNormal(eyeNormal);

    // Compute a noise rotation matrix
    let noiseRotation = rotationMatrixFor(signedRandomNoise(uv*123456.0f));

    // Compute the full sample tangent to view matrix.
    let sampleTangentToView = TBN*noiseRotation;

	let outSampleCount = 0.0f;

    for(let i = 0; i < SamplingDistributionSize; i += 1)
    {
        let delta = sampleTangentToView*(SamplingDistribution[i]*0.1f);
        outSampleCount += sampleAmbientOcclusionAt(eyeDepth, eyePosition + delta);
    }

	let value = 1.0f - outSampleCount / float(SamplingDistributionSize);
    FragmentStage.colorOutput0 = float4(value, 0.0f, 0.0f, 0.0f);
}
