WMVector3 class category: 'construction' methods: {
:[x: <Scalar>x -> WMVector3.
	^ WMVector3 newValue
		x: x;
		yourself
].

:[y: <Scalar>y -> WMVector3.
	^ WMVector3 newValue
		y: y;
		yourself
].

:[z: <Scalar>z -> WMVector3.
	^ WMVector3 newValue
		z: z;
		yourself
].

:[x: <Scalar>x y: <Scalar>y z: <Scalar>z -> WMVector3.
	^ WMVector3 newValue
		x: x;
		y: y;
		z: z;
		yourself
].

}.

WMVector3 category: 'arithmetic' methods: {
:[+ <WMVector3> other -> WMVector3.
	^ WMVector3 x: x + other x y: y + other y z: z + other z
].

:[- <WMVector3> other -> WMVector3.
	^ WMVector3 x: x - other x y: y - other y z: z - other z
].

:[dot: <WMVector3> other -> Scalar.
	^ (x * other x) + (y * other y) + (z * other z)
].

:[length2 -> Scalar.
	^ (x * x) + (y * y) + (z * z)
].

:[length -> Scalar.
	^ ((x * x) + (y * y) + (z * z)) sqrt
].

}.

WMVector3 category: 'comparing' methods: {
:[= other -> BooleanBit.
	^ WMVector3 == other class and: [x = other x and: [y = other y and: [z = other z]]]
].

:[hash -> _DynamicObject.
	^ (x hash bitXor: y hash) bitXor: z hash
].

:[closeTo: <WMVector3> other -> BooleanBit.
	^ (x closeTo: other x) and: [(y closeTo: other y) and: [z closeTo: other z]]
].
}.

WMVector3 category: 'printing' methods: {
:[printOn: aStream
	aStream nextPutAll: WMVector3 name;
		nextPutAll: ' x: '; print: x;
		nextPutAll: ' y: '; print: y;
		nextPutAll: ' z: '; print: z.
].
}.
